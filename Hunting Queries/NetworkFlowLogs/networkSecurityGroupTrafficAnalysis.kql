// IPv4 NSG Flow Log Search 
AzureNetworkAnalytics_CL 
| where SubType_s == "FlowLog"
| extend FlowDirection = iff(FlowDirection_s == 'O', 'Outbound', 'Inbound')
| extend AllowedOrDenied = iff(FlowStatus_s == 'A', 'Allowed', 'Denied')
| extend SourceIP = iff(isempty(SrcIP_s), extract_all(@"(\d{1,3}\.\d{1,3}\.\d{1,3}\.\d{1,3})", SrcPublicIPs_s), SrcIP_s)
| extend DestinationIP = iff(isempty(DestIP_s), extract_all(@"(\d{1,3}\.\d{1,3}\.\d{1,3}\.\d{1,3})", DestPublicIPs_s), DestIP_s)
| extend Protocol = case(L4Protocol_s == 'T', "TCP", L4Protocol_s == 'U', "UDP", L4Protocol_s)
| project-rename NSGFL_Version = FASchemaVersion_s
| project
    TimeGenerated,
    FlowDirection,
    AllowedOrDenied,
    SourceIP,
    DestinationIP,
    DestPort_d,
    Protocol,
    L7Protocol_s,
    NSGList_s,
    NSGRule_s,
    NSGFL_Version
//| where SourceIP contains "XXX.XXX.XXX.XXX" and DestPort_d == XXXXX
